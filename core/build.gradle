plugins {
    id("project-defaults")
}

configurations {
    dynamodb
}

repositories {
    maven {
        url("http://dynamodb-local.s3-website-us-west-2.amazonaws.com/release")
    }
}

dependencies {
    implementation("com.amazonaws:aws-java-sdk-dynamodb")
    implementation("com.fasterxml.jackson.core:jackson-annotations")
    implementation("com.fasterxml.jackson.core:jackson-databind")
    implementation("org.apache.commons:commons-lang3:3.10")
    implementation("cafe.cryptography:ed25519-elisabeth:0.1.0")

    testImplementation("com.amazonaws:DynamoDBLocal:1.11.477")
    testImplementation("com.fasterxml.jackson.module:jackson-module-kotlin")

    dynamodb(fileTree(dir: 'lib', include: ["*.dylib", "*.so", "*.dll"]))
    dynamodb("com.amazonaws:DynamoDBLocal:1.11.477")
}

task copyNativeDeps(type: Copy) {
    from(configurations.dynamodb)
    into("${project.buildDir}/libs")
}

test.dependsOn(copyNativeDeps)
test.doFirst {
    systemProperty("java.library.path", "./build/libs")
}